plugins {
    id 'java'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'me.th3unkn0wn1'
version = '1.0'

repositories {
    mavenCentral()

    // Paper Repository
    maven {
        url = 'https://repo.papermc.io/repository/maven-public/'
    }

    // PlaceholderAPI
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    // FastAsyncWorldEdit (FAWE)
    maven {
        name = 'IntellectualSites'
        url = 'https://mvn.intellectualsites.com/content/groups/public/'
    }
}

dependencies {
    // Paper API
    compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'

    // PlaceholderAPI
    compileOnly 'me.clip:placeholderapi:2.11.6'

    // FastAsyncWorldEdit
    compileOnly 'com.fastasyncworldedit:FastAsyncWorldEdit-Core:2.9.2'
    compileOnly('com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit:2.9.2') {
        transitive = false
    }
}

tasks.named('runServer') {
    // Configure the Minecraft version for our task.
    minecraftVersion('1.21.1')
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}